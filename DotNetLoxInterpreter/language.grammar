program        -> declaration* EOF ; 

declaration    -> varDecl
                | statement ;

varDecl        -> "var" IDENTIFIER ( "=" expresion )? ";" ;

statement      -> exprStmt
                | printStmt 
                | block ;

block          -> "{" declaration* "}" ;

printStmt      -> "print" expression ";" ;
exprStmt       -> expresion ";" ;

expression     -> assignment ;

assignment     -> IDENTIFIER "=" assignment | commaseq ;
commaseq       -> ternary ( "," ternary )* ;
ternary        -> equality ( "?" ternary ":" ternary )? ;
equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           -> factor ( ( "-" | "+" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary | primary ;
primary        -> NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" | IDENTIFIER ;